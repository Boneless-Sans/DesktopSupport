plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group 'org.madlonkay'
version '0.4.0-SNAPSHOT'

dependencies {
    // This should really be an 'api' dependency, but shadow plugin bugs out:
    // https://github.com/johnrengelman/shadow/issues/473
    compile project(':lib-api')
    implementation project(':lib-java9')
    implementation project(':lib-apple')

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

tasks.withType(Jar) {
    archiveBaseName = 'desktopsupport'
}

jar {
    archiveClassifier = 'single'
}

shadowJar {
    archiveClassifier = ''
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    doFirst{
        rootProject.subprojects.findAll { it.name.startsWith('lib-') }.each { proj ->
            from proj.sourceSets.main.allJava
        }
    }
}

javadoc {
    title = "DesktopSupport $version"
    doFirst{
        rootProject.subprojects.findAll { it.name.startsWith('lib-') }.each { proj ->
            source += proj.sourceSets.main.allJava
            classpath += proj.sourceSets.main.compileClasspath
        }
    }
}

task javadocJar(type: Jar) {
    from javadoc.outputs
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        myPublication(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId 'desktopsupport'
            artifact sourceJar
            artifact javadocJar
        }
    }
}

bintray {
    user = rootProject.findProperty('bintrayUser')
    key = rootProject.findProperty('bintrayKey')
    publications = ['myPublication']
    pkg {
        repo = 'maven'
        name = 'DesktopSupport'
    }
}
