plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group 'org.madlonkay'
version '0.6.0'

dependencies {
    // This should really be an 'api' dependency, but shadow plugin bugs out:
    // https://github.com/johnrengelman/shadow/issues/473
    compile project(':lib-api')
    implementation project(':lib-java9')
    implementation project(':lib-apple')

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

tasks.withType(Jar) {
    archiveBaseName = 'desktopsupport'
}

jar {
    archiveClassifier = 'single'
}

shadowJar {
    archiveClassifier = ''
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    doFirst{
        rootProject.subprojects.findAll { it.name.startsWith('lib-') }.each { proj ->
            from proj.sourceSets.main.allJava
        }
    }
}

javadoc {
    title = "DesktopSupport $version"
    doFirst{
        rootProject.subprojects.findAll { it.name.startsWith('lib-') }.each { proj ->
            source += proj.sourceSets.main.allJava
            classpath += proj.sourceSets.main.compileClasspath
        }
    }
}

task javadocJar(type: Jar) {
    from javadoc.outputs
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'desktopsupport'
            artifact sourceJar
            artifact javadocJar

            pom {
                name = 'DesktopSupport'
                description = 'A library to bridge Apple Java Extensions and JEP 272 desktop APIs'
                url = 'https://github.com/amake/DesktopSupport'
                scm {
                    connection = 'scm:git:https://github.com/amake/DesktopSupport'
                    developerConnection = 'scm:git:https://github.com/amake/DesktopSupport'
                    url = 'https://github.com/amake/DesktopSupport.git'
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'amake'
                        name = 'Aaron Madlon-Kay'
                        email = 'aaron@madlon-kay.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials(PasswordCredentials) {
                username rootProject.findProperty('ossrhUsername')
                password rootProject.findProperty('ossrhPassword')
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
