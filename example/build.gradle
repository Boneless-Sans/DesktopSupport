import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'application'
    id 'eclipse'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    jcenter()
}

dependencies {
    implementation project(path: ':lib', configuration: 'shadow')

    testImplementation 'junit:junit:4.12'
}

mainClassName = 'org.madlonkay.desktopsupport.example.CliApp'

ext {
    appApplicationName = 'guiapp'
    appWrapper = "${appApplicationName}wrapper"
}

distributions {
  app {
    contents {
        into("DesktopSupportExampleApp.app/Contents/MacOS") {
            with main.contents
        }
        from(projectDir) {
            include 'Info.plist'
            into "DesktopSupportExampleApp.app/Contents"
            filter(ReplaceTokens, tokens: [WRAPPER_NAME: appWrapper.toString()])
        }
        from(projectDir) {
            include 'guiappwrapper'
            into "DesktopSupportExampleApp.app/Contents/MacOS/$executableDir"
            filter(ReplaceTokens, tokens: [APPLICATION_NAME: appApplicationName])
        }
    }
  }
}

task appScripts(type: CreateStartScripts) {
    applicationName = appApplicationName
    mainClassName = 'org.madlonkay.desktopsupport.example.GuiApp'
    outputDir = file("$buildDir/appScripts")
    classpath = startScripts.classpath
}

applicationDistribution.from(appScripts) {
    into executableDir
}

// Hack to shut up errors in Eclipse about missing lib-api class.
eclipse {
    classpath {
        file {
            withXml {
                def node = it.asNode()
                node.appendNode('classpathentry', [kind: 'src', path: '/lib-api'])
            }
        }
    }
}
